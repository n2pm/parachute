plugins {
    id "maven-publish"
    alias(libs.plugins.quilt.loom)
    alias(libs.plugins.minotaur)
}

archivesBaseName = project.archives_base_name + "-" + libs.versions.minecraft.get()
version = project.version
group = project.maven_group

def build_release = System.getenv("BUILD_RELEASE") == "true"
def build_ci_commit = System.getenv("SHA")

version = project.mod_version + ""

if (!build_release) {
    version = project.mod_version + "+" + new Date().format("yyyyMMdd.HHmmss")
}

if (build_ci_commit != null) {
    version = project.mod_version + "+${build_ci_commit}"
}

repositories {
    maven { url "https://masa.dy.fi/maven" }
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven { url "https://maven.gegy.dev/" }
    mavenCentral()
}

dependencies {
    minecraft libs.minecraft
    mappings loom.layered {
        addLayer quiltMappings.mappings("org.quiltmc:quilt-mappings:${libs.versions.quilt.mappings.get()}:v2")
    }
    modImplementation libs.quilt.loader

    modImplementation libs.quilted.fabric.api

    implementation "com.github.bbottema:jetbrains-runtime-annotations:1.0.0"

    // probably a bad idea to jij this lol
    modApi include("fi.dy.masa.malilib:malilib-fabric-1.19.1:${libs.versions.malilib.get()}")
    modCompileOnly("com.terraformersmc:modmenu:${libs.versions.mod.menu.get()}")

    // todo: don't load from fs
    modImplementation files("./libs/cauldron-mc1.19.1-0.1.2-alpha2.jar")
    include files("./libs/cauldron-mc1.19.1-0.1.2-alpha2.jar")
}

processResources {
    inputs.property "version", version

    filesMatching("quilt.mod.json") {
        expand project.properties
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

modrinth {
    syncBodyFrom = rootProject.file("README.md").text
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "parachute"
    versionNumber = version
    versionType = build_release ? "release" : "alpha"
    uploadFile = remapJar
    gameVersions = ["1.19.1"]
    loaders = ["quilt"]
    dependencies {
        required.project "qsl"
    }
}

tasks.modrinth.dependsOn(tasks.modrinthSyncBody)
